defmodule Livemd2exs do
  @moduledoc """
  Converts Livebook (`.livemd`) files to executable Elixir scripts (`.exs`).

  Only Elixir code blocks are extracted; text blocks and other content are
  discarded. The generated script includes a shebang header with a timestamp
  and source file path.
  """

  @doc """
  Extracts Elixir code blocks from a Livebook file and returns an executable script string.

  The file at `livemd_path` is parsed with Earmark, and all fenced code blocks
  are extracted, joined with blank lines, and prepended with a header.

  ## Examples

      iex> script = Livemd2exs.extract("test/fixtures/simple.livemd")
      iex> String.starts_with?(script, "#! /usr/bin/env elixir")
      true

  """
  def extract(livemd_path) do
    text = File.read!(livemd_path)
    {:ok, ast, _warnings} = Earmark.as_ast(text)

    header = """
    #! /usr/bin/env elixir
    #
    # Generated by `livemd2exs` on #{now_str()}
    # livemd input: #{livemd_path}
    #

    """

    script =
      ast
      |> Enum.filter(fn
        {"pre", _, _, _} -> true
        _ -> false
      end)
      |> Enum.map(fn {_, _, [code], _} -> code end)
      |> Enum.map(fn {_, _, [code], _} -> code end)
      |> Enum.join("\n\n")

    header <> script
  end

  defp now_str do
    case System.find_executable("date") do
      nil ->
        DateTime.utc_now() |> Calendar.strftime("%Y-%m-%d %H:%M:%S UTC")

      _path ->
        {output, 0} = System.cmd("date", ["+%Y-%m-%d %H:%M:%S %Z"])
        String.trim(output)
    end
  end
end
